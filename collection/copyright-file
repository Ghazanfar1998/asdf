#!/usr/bin/perl -w
# copyright-file -- lintian collector script

# Copyright (C) 1998 Richard Braakman
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

use strict;

use lib "$ENV{'LINTIAN_ROOT'}/lib";
use Util;

use File::Copy qw(copy);

($#ARGV == 1) or fail("syntax: copyright-file <pkg> <type>");
my $pkg = shift;
my $type = shift;

-f "fields/package" or fail("copyright-file invoked in wrong directory");

unlink("copyright");

my $file1 = "unpacked/usr/share/doc/$pkg/copyright";
my $file2 = "unpacked/usr/doc/$pkg/copyright";
my $docdir = 'usr/share/doc';

my $file;
if (-f $file1 ) { $file = $file1; }
else {
    $file = $file2;
    $docdir = 'usr/doc';
}

if (-d "unpacked/$docdir/$pkg"
      && !is_ancestor_of("unpacked", "unpacked/$docdir/$pkg")) {
    # if the parent dir is outside the package, just stop here before we
    # do something we will regret.
    touch_file("copyright");
    return;
}

# If copyright was a symlink, we need to make a copy of it.  Just hardlinking
# to the symlink may leave a relative symlink into a directory we can't
# unpack.  Be careful about what symlinks we allow, though.
if (-l $file) {
    my $link = readlink($file) or fail("cannot readlink $file: $!");
    if ($link =~ /\.\./ || ($link =~ m%/% && $link !~ m%^[^/]+(/+[^/]+)*\z%)) {
        touch_file("copyright");
    } else {
        copy($file, "copyright") or fail("cannot copy $file: $!");
    }
} elsif (-f $file) {
    link($file, "copyright")
	or fail("cannot link $file to copyright: $!");
} elsif (-f "$file.gz") {
    gunzip_file($file, 'copyright');
} else {
    # no copyright file found
    touch_file('copyright');
}
